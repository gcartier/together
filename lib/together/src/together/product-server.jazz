;;;============
;;;  Together
;;;============
;;;
;;;; Together-Server Product
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module together.product-server jazz


(import (jazz.io)
        (jazz.product)
        (together.product-base))


;;;
;;;; Build
;;;


(definition (build descriptor (unit: unit #f) (force?: force? #f))
  (define (resources-dir)
    (let ((bundle (product-descriptor-build-bundle descriptor)))
      (if bundle
          (new-directory {Directory Build} (format "{a}.app/Contents/Resources" bundle))
        {Directory Build})))
  
  (let ((project-dir {Directory Together})
        (resources-dir (resources-dir)))
    (define (install-filename path)
      (let ((src (new-file project-dir path))
            (dst (new-file resources-dir path)))
        (install-file src dst
          feedback: (lambda (action pathname)
                      (build-feedback "; {a} {a}..." action (get-name pathname))))))
    
    (define (install-dirname path (src #f) (dst #f))
      (let ((src (new-directory project-dir (or src path)))
            (dst (new-directory resources-dir (or dst path))))
        (install-directory src dst
          feedback: (let ((first? #t))
                      (lambda (action pathname)
                        (when first?
                          (build-feedback "; {a} {a}..." action (get-name pathname))
                          (set! first? #f)))))))
    
    (define (copy-info)
      (install-filename '("README"))
      (install-filename '("devel" "together" "About.ftx"))
      (install-filename '("devel" "together" "Help.ftx"))
      (install-filename '("devel" "together" "News.ftx"))
      (install-filename '("devel" "together" "Roadmap.ftx"))
      (install-filename '("devel" "together" "Unverified.ftx")))
    
    (define (copy-assets)
      (install-dirname '("lib" "together" "assets")))
    
    (define (copy-templates)
      (install-dirname '("devel" "templates")))
    
    (build-product-descriptor descriptor unit: unit force?: force?)
    (initialize-aliases)
    (generate-build-metadata ".together-server")
    (copy-info)
    (copy-assets)
    (copy-templates)))


;;;
;;;; Install
;;;


(definition install-dir
  (cond-expand
    (mac
     {Directory Home "Devel" "together" "install"})
    (windows
     {Directory Home "together" "install"})
    (else
     {Directory :root "mnt" "disks" "storage" "devel" "together" "install"})))


(definition install-name
  (cond-expand (devel
                "togetherserver-devel")
               (trial
                "togetherserver-trial")
               (test
                "togetherserver-test")
               (triage
                "togetherserver-triage")
               (stable
                "togetherserver-stable")
               (stage
                "togetherserver-stage")
               (prod
                "togetherserver-prod")))


(definition (install descriptor)
  (initialize-aliases)
  ;; appl
  (install-application descriptor install-dir
    name: install-name
    scriptable?: #t
    platform?: #t
    opengl?: #t
    files: (cond-expand
             (windows '("README" "together-server.exe" "together-processor.exe" "together-worker.exe"))
             (linux '("README" "Together-Server" "Together-Processor" "Together-Worker"))
             (else '("README")))
    directories: '("devel")
    metadata: '(".together-server" ".together-processor" ".together-worker"))
  ;; apps
  (cond-expand
    (mac
     (let ((app-dir (new-directory install-dir install-name)))
       (install-directory {Directory Build "together-server.app"} (new-directory app-dir '("Contents" "MacOS" "together-server.app"))
         feedback: (lambda (action pathname)
                     (build-feedback "; {a} {a}..." action (get-name pathname))))
       (install-directory {Directory Build "together-processor.app"} (new-directory app-dir '("Contents" "MacOS" "together-processor.app"))
         feedback: (lambda (action pathname)
                     (build-feedback "; {a} {a}..." action (get-name pathname))))
       (install-directory {Directory Build "together-worker.app"} (new-directory app-dir '("Contents" "MacOS" "together-worker.app"))
         feedback: (lambda (action pathname)
                     (build-feedback "; {a} {a}..." action (get-name pathname))))))
    (else)))


;;;
;;;; Deploy
;;;


(definition (deploy descriptor)
  (initialize-aliases)
  ;; appl
  (deploy-application descriptor
                      (new-directory install-dir install-name)
                      make: 'together-all
                      install: 'together-server))


;;;
;;;; Register
;;;


(register-product 'together-server
  build: build
  install: install
  deploy: deploy))
