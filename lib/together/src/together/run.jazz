;;;============
;;;  Together
;;;============
;;;
;;;; Together Run
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module together.run jazz


(import (jazz.action)
        (jazz.git.foreign)
        (jazz.io)
        (jazz.logger)
        (jazz.platform)
        (jazz.settings)
        (jazz.snapshot)
        (world)
        (world.build)
        (world.profiling)
        (together)
        (together.domain))


(definition (prepare-logger)
  (initialize-aliases)
  (set-logger-directory (new-directory (determine-settings-root) '("1.0.0" "work" "logs")))
  (set-logger-prefix "client"))


(definition protected (prepare-together)
  (set-together? #t)
  (set-window? #t)
  (set-tier-title "Together")
  (set-tier-kind 'client)
  (set-process-kind 'application)
  (set-settings-alias 'Resources)
  (set-settings-root (determine-settings-root))
  (set-documents-root (determine-documents-root))
  (set-options-setup setup-options)
  (set-settings-setup setup-settings)
  (set-settings-override override-settings)
  (set-aliases-setup setup-aliases)
  (set-valid-roles '(user developer))
  (set-default-server "Together")
  (set-server-filename (determine-server-filename))
  (set-worker-filename (determine-worker-filename))
  (set-action-item-visibility #f)
  (snapshot-prefix "Together")
  (register-domain-names)
  (register-binaries)
  (setup-build ".together")
  (setup-presence)
  (setup-profiling)
  (advise-sites))


(definition (register-binaries)
  (when (equal? (string-argument "called-from" #f)
                "installer")
    (let ((dirname (cond-expand
                     (mac kernel-bundle-root)
                     (else kernel-install))))
      (let ((dir (new Directory (tokenise-filename dirname))))
        (when (exists? dir)
          (let ((digest (repository-head-digest dir)))
            (add-snapshot-property 'binaries digest)))))))


(definition protected (prepare-settings)
  (load-unit 'world.settings)
  (when start-tier-listener
    (start-tier-listener))
  (when connect-slave-to-master
    (connect-slave-to-master)))


(definition (setup-options settings)
  (for-each-property (lambda (property value)
                       (set-setting settings property value))
                     '(;; media
                       world.video-layout         circle)))


(definition (setup-settings settings)
  (for-each-property (lambda (property value)
                       (set-setting settings property value))
                     '(;; role
                       world.role                 user
                       ;; configure
                       world.render-rate          30.
                       world.render-rate-inactive 30.
                       world.post-processing?     #t
                       world.multisampling        1
                       ;; keys
                       world.arrow-movement?      #f
                       world.move-up              {Shortcut #\R}
                       ;; test
                       world.init-script          #f
                       world.test-script          #f
                       world.test1-script         #f
                       world.test2-script         #f
                       world.test3-script         #f
                       world.test4-script         #f
                       world.test5-script         #f
                       world.test6-script         #f
                       world.test7-script         #f
                       world.test8-script         #f
                       world.test9-script         #f
                       world.test0-script         #f
                       ;; worker
                       world.worker?              #f)))


(definition (override-settings settings)
  (for-each-property (lambda (property value)
                       (set-setting settings property value))
                     '(;; interface
                       world.show-belt?           #f
                       ;; audio
                       world.music?               #f
                       world.ambience?            #f
                       ;; video
                       world.sun-cycle?           #f
                       world.start-time           day
                       ;; generation
                       world.tile-pack            "minecraft"
                       world.tile-resolution      16
                       world.generate-mipmap?     #t
                       world.min-filter           linear
                       world.mag-filter           nearest)))


(definition (create-documents)
  (create-directories {Directory Documents "assets"})
  (create-directories {Directory Documents "shared"})
  (create-directories {Directory Documents "streams"}))


(definition (install-valid?)
  (define (inside-installer?)
    (cond-expand
      (mac
       (and kernel-bundle-root
            (let ((together (new Directory (tokenise-filename kernel-bundle-root))))
              (define (together-app? name)
                (and (starts-with? name "Together")
                     (ends-with? name ".app")))
              
              (and (together-app? (get-name together))
                   (let ((apps (get-parent together)))
                     (and (filename=? (get-name apps) "Apps")
                          (exists? (new-directory apps "Update.app"))
                          (let ((contents (get-parent apps)))
                            (and (filename=? (get-name contents) "Contents")
                                 (exists? (new-directory contents "MacOS"))
                                 (exists? (new-directory contents "Worlds"))
                                 (let ((installer (get-parent contents)))
                                   (together-app? (get-name installer)))))))))))
      (else
       (let ((together (new Directory (tokenise-filename kernel-install))))
         (and (filename=? (get-name together) "together")
              (let ((app (get-parent together)))
                (and (filename=? (get-name app) "app")
                     (exists? (new-directory app "update"))
                     (let ((installer (get-parent app)))
                       (and (exists? (new-directory installer "worlds"))
                            (exists? (new-file installer (add-extension "together" (executable-extension kernel-platform)))))))))))))
  
  (if (and (inside-installer?)
           (not (equal? (string-argument "called-from" #f)
                        "installer")))
      (let ((color (cond-expand
                     (triage "red")
                     (else "green"))))
        (system-message (format "Please use the {a} icon to launch Together" color)
                        type: 'problem
                        title: "Together")
        #f)
    #t))


(definition (run-together descriptor)
  (when (install-valid?)
    (prepare-logger)
    (prepare-together)
    (prepare-settings)
    (create-documents)
    (load-unit 'world.work)
    (load-unit 'together.boot)
    (boot-process)))


(register-product-run 'together
  run-together))
