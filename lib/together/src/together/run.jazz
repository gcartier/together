;;;============
;;;  Together
;;;============
;;;
;;;; Together Run
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module together.run jazz


(import (jazz.action)
        (jazz.foreign)
        (jazz.graphic.image)
        (jazz.io)
        (jazz.logger)
        (jazz.platform)
        (jazz.presence)
        (jazz.settings)
        (jazz.snapshot)
        (jazz.version)
        (world)
        (world.build)
        (world.foreign)
        (together)
        (together.domain))


(definition (prepare-limits)
  (cond-expand
    (mac
     (set-rlimit-nofile 1024))
    (else)))


(definition (prepare-logger)
  (initialize-aliases)
  (set-logger-directory (new-directory (determine-settings-root) '("1.0.0" "work" "logs")))
  (set-logger-prefix "client"))


(definition protected (prepare-together)
  (set-window? #t)
  (set-tier-title "Together")
  (set-tier-kind 'client)
  (set-process-kind 'application)
  (set-settings-alias 'Resources)
  (set-settings-root (determine-settings-root))
  (set-documents-root (determine-documents-root))
  (set-universe-root (determine-universe-root))
  (set-options-setup setup-options)
  (set-settings-setup setup-settings)
  (set-settings-override override-settings)
  (set-aliases-setup setup-aliases)
  (set-valid-roles '(user builder scripter developer))
  (set-server-filename (determine-server-filename))
  (set-worker-filename (determine-worker-filename))
  (set-action-item-visibility #f)
  (snapshot-prefix "Together")
  (cond-expand-features-add! 'client)
  (register-domain-names)
  (register-kernel-properties)
  (register-platform-properties)
  (setup-build-metadata ".together")
  (setup-presence))


(definition (register-kernel-properties)
  (add-snapshot-property 'kernel.platform kernel-platform)
  (add-snapshot-property 'kernel.features kernel-features)
  (add-snapshot-property 'kernel.properties kernel-properties))


(definition (register-platform-properties)
  (add-snapshot-property 'platform.version (present (get-platform-version))))


(definition protected (prepare-settings)
  (load-unit 'world.settings)
  (when start-tier-listener
    (start-tier-listener))
  (when connect-slave-to-master
    (connect-slave-to-master)))


(definition (setup-options settings)
  (for-each-property (lambda (property value)
                       (set-setting settings property value))
                     '(;; render
                       world.view-distance         7
                       world.multisampling?        #t
                       world.multisampling         2
                       world.render-rate           #f
                       world.render-rate-inactive  #f
                       ;; media
                       world.video-layout          circle)))


(definition (setup-settings settings)
  (for-each-property (lambda (property value)
                       (set-setting settings property value))
                     '(;; render
                       world.tick-rate-inactive    .75
                       world.message-rate-inactive 5.
                       ;; test
                       world.init-script           #f
                       world.test-script           #f
                       world.test1-script          #f
                       world.test2-script          #f
                       world.test3-script          #f
                       world.test4-script          #f
                       world.test5-script          #f
                       world.test6-script          #f
                       world.test7-script          #f
                       world.test8-script          #f
                       world.test9-script          #f
                       world.test0-script          #f)))


(definition (override-settings settings)
  (for-each-property (lambda (property value)
                       (set-setting settings property value))
                     '(;; interface
                       world.show-belt?            #f
                       ;; audio
                       world.music?                #f
                       world.ambience?             #f)))


(definition (prepare-stack)
  (thread-add-stack (primordial-thread) stack-size)
  (set-presence-execute-runner
    (lambda (thunk name)
      (make-thread-with-stack stack-size thunk name))))


(definition (create-documents)
  (create-directories {Directory Documents "assets"})
  (create-directories {Directory Documents "assets" "model"})
  (create-directories {Directory Documents "assets" "painting"})
  @wait-till-there-is-a-user-way-to-share-documents
  (create-directories {Directory Documents "shared"})
  (create-directories {Directory Documents "streams"}))


(definition protected (load-together)
  (prepare-limits)
  (prepare-logger)
  (prepare-together)
  (prepare-settings)
  (prepare-stack)
  (load-unit 'world.profiling)
  (create-documents)
  (load-unit 'world.work)
  (load-unit 'together.boot)
  (load-unit 'together.load))


(definition protected (boot-together)
  (boot-process))


(definition protected (run-together)
  (load-together)
  (when (find-setting 'world.persist-allocations? #f)
    (persist-allocations))
  (boot-together)))
