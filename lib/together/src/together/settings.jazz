;;;============
;;;  Together
;;;============
;;;
;;;; Together Settings
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module together.settings jazz


(import (jazz.debuggee)
        (jazz.io)
        (jazz.platform)
        (jazz.settings)
        (world)
        (world.autoload)
        (world.io)
        (world.profile)
        (world.settings)
        (together.exception))


(definition protected (together-file)
  (let ((user (string-argument "user" "Together")))
    (new-file {Directory Settings} (list "clients" user ".client"))))


;;;
;;;; Welcome
;;;


(definition protected (open-login-zone)
  (open-standalone (find-tier (copy-zone-template "welcome" 'standalone) 'standalone)))


(definition protected (open-welcome/circle-zone (force-welcome?: force-welcome? #f))
  (define (open-circle remote-file)
    (let ((obj (open-client remote-file)))
      (if (is? obj Zone)
          obj
        (let ((title (if (string? obj) obj "Unable to connect to server"))
              (zone (open-login-zone)))
          (report-exception zone title obj)))))
  
  (let ((file (together-file)))
    (if (or force-welcome?
            (not (exists? file))
            (shift-down?))
        (open-login-zone)
      (open-circle file))))


(welcome-zone-open-set! open-welcome/circle-zone)


;;;
;;;; World
;;;


(definition protected (find-world)
  (define (determine-world)
    (let ((worlds (determine-worlds)))
      (and worlds
           (let ((world (new-directory worlds "together")))
             (and (exists? world)
                  world)))))
  
  (define (determine-worlds)
    (and (exists? {Directory Application})
         (let ((parent (get-parent {Directory Application})))
           (and (and parent (exists? parent) (not (meta-root? parent)))
                (let ((parent (get-parent parent)))
                  (and (and parent (exists? parent) (not (meta-root? parent)))
                       (let ((worlds (new-directory parent "worlds")))
                         (if (exists? worlds)
                             worlds
                           (let ((worlds (new-directory parent "Worlds")))
                             (if (exists? worlds)
                                 worlds
                               #f))))))))))
  
  (let ((world (determine-world)))
    (cond ((not world)
           (system-message "Unable to find Together's world"
                           type: 'problem
                           title: "Together")
           (exit 1))
          ((not (exists? world))
           (system-message (format "Unable to find Together's world: {a}" (parse world))
                           type: 'problem
                           title: "Together")
           (exit 1))
          (else
           world))))


(set-world-finder find-world)


;;;
;;;; Skybox
;;;


(definition protected together-skybox
  (or (world-setting 'together.skybox (get-skybox (active-graphic-profile)))
      (if (world-setting 'together.first-use? #t)
          (begin
            (set-setting options-settings 'together.first-use? #f)
            (save-content options-settings)
            "nebula")
        (random-element '("calm_sea" "dark_sea" "nebula")))))


;;;
;;;; Exceptions
;;;


(unless (or (using-debugger?) (controlling-terminal?))
  (setup-together-exception-debugger)))
