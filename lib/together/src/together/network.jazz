;;;============
;;;  Together
;;;============
;;;
;;;; Network Profile
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module together.network jazz


(import (jazz.event)
        (jazz.graphic)
        (jazz.io)
        (jazz.time)
        (jazz.view)
        (world)
        (world.client.tier)
        (world.client.udp)
        (world.configure)
        (world.context)
        (world.event)
        (world.evolution)
        (world.interface.evolution)
        (world.interface.timeline)
        (world.settings)
        (world.streaming)
        (world.task)
        (world.udp)
        (world.video)
        (together))


(definition protected heartbeat-upload
  0)

(definition protected heartbeat-download
  0)

(definition protected heartbeat-values
  '(0 100 200 500 1000 2000 5000 10000 20000))

(definition protected (cycle-heartbeat-upload (reversed?: reversed? #f))
  (set! heartbeat-upload (or (cycle-element heartbeat-values heartbeat-upload cycle?: #f reversed?: reversed?) heartbeat-upload))
  heartbeat-upload)

(definition protected (cycle-heartbeat-download (reversed?: reversed? #f))
  (set! heartbeat-download (or (cycle-element heartbeat-values heartbeat-download cycle?: #f reversed?: reversed?) heartbeat-download))
  heartbeat-download)


(definition protected running-profile
  #f)


(class Network-Profile extends Object
  
  
  (slot panel            getter generate)
  (slot receive-thread   getter generate)
  (slot heartbeat-thread getter generate)
  (slot redraw-thread    getter generate)
  (slot send-thread      getter generate)
  
  
  (method override (initialize self panel receive-thread heartbeat-thread redraw-thread send-thread)
    (nextmethod self)
    (set! self.panel panel)
    (set! self.receive-thread receive-thread)
    (set! self.heartbeat-thread heartbeat-thread)
    (set! self.redraw-thread redraw-thread)
    (set! self.send-thread send-thread)))


(class Network-Timeline extends Timeline-Panel
  
  
  (method override (timeline-start self) <fl>
    (evolutionary-time evolution (first-position evolution)))
  
  
  (method override (timeline-end self) <fl>
    (evolutionary-time evolution (last-position evolution))))


(definition protected (profile-network (heartbeat?: heartbeat? #f))
  (let ((interface (current-interface))
        (client (current-client))
        (world (current-world))
        (zone (current-zone)))
    (define (feedback message (duration #f))
      (display-message world message duration: duration))
    
    (feedback "Profile network..." +inf.0)
    (start-profile client)
    (let ((udp-client (get-udp-client client))
          (heartbeat-period (/ 1. 10.))
          (redraw-period (/ 1. 2.))
          (base (current-monotonic))
          (recuperate 1.)
          (panel (new Network-Timeline parent: interface location: '(center 155) proportion: '((bounds -500) 120))))
      (define (wait target)
        (sleep (- (+ base target) (current-monotonic))))
      
      (set-replay? panel #t)
      (set-show-knees? panel #t)
      (set-border-color panel {Color Dark-Gray})
      (set-chronology panel (get-profile-chronology udp-client))
      (set-evolution panel (get-profile-evolution udp-client))
      (layout-view interface)
      (let ((receive-thread
              (new-thread
                (lambda ()
                  (declare (proper-tail-calls))
                  (let ((udp-port (get-udp-port udp-client)))
                    (let (loop)
                      (let ((data (read udp-port)))
                        (receive-data udp-client data))
                      (loop))))
                'receive))
            (heartbeat-thread
              (new-thread
                (lambda ()
                  (declare (proper-tail-calls))
                  (let (loop (n 0))
                    (send-profile-heartbeat udp-client)
                    (wait (* (cast <fl> n) heartbeat-period))
                    (loop (+ n 1))))
                'heartbeat))
            (redraw-thread
              (new-thread
                (lambda ()
                  (declare (proper-tail-calls))
                  (let (loop (n 0))
                    (delay-event
                      (lambda ()
                        (with-task-mutex
                          (lambda ()
                            (redraw panel)))))
                    (wait (* (cast <fl> n) redraw-period))
                    (loop (+ n 1))))
                'redraw)))
        (let ((send-thread
                (and (or (not heartbeat?)
                         (> heartbeat-upload 0))
                     (new-thread
                       (lambda ()
                         (let ((start 0.))
                           (define (profile-upload mbps (duration: duration #f) (feedback?: feedback? #t))
                             (when feedback?
                               (feedback (format "Profile upload {a}Mbps..." mbps) +inf.0))
                             (let ((size 1000)
                                   (duration (or duration 1.)))
                               (let ((delay (/ (cast <fl> size) (mbps->bytes mbps))))
                                 (let ((iter (fxround (/ duration delay))))
                                   (let ((elapse (/ duration (cast <fl> iter)))
                                         (data (make-u8vector size)))
                                     (loop (for n from 0 below iter)
                                           (wait (+ start (* (cast <fl> n) elapse)))
                                           (send-profile-media udp-client data)))))
                               (increase! start duration))
                             (wait (+ start recuperate))
                             (increase! start recuperate))
                           
                           (define (profile-download mbps (feedback?: feedback? #t))
                             (when feedback?
                               (feedback (format "Profile download {a}Mbps..." mbps) +inf.0))
                             (test-profile client 'download (list mbps))
                             (wait (+ start 2.))
                             (increase! start 2.))
                           
                           (define (profile-both mbps)
                             (feedback (format "Profile both {a}Mbps..." mbps) +inf.0)
                             (test-profile client 'download (list mbps))
                             (profile-upload mbps feedback?: #f))
                           
                           (define (profile-bandwiths profile)
                             (profile  1)
                             (profile  2)
                             (profile  4)
                             (profile  8)
                             (profile 16))
                           
                           (declare (proper-tail-calls))
                           (cond (heartbeat?
                                  (when (> heartbeat-download 0)
                                    (test-profile client 'download (list heartbeat-download)))
                                  (when (> heartbeat-upload 0)
                                    (profile-upload heartbeat-upload feedback?: #f)))
                                 (else
                                  (wait (+ start recuperate))
                                  (increase! start recuperate)
                                  (profile-bandwiths profile-upload)
                                  (profile-bandwiths profile-download)
                                  (profile-bandwiths profile-both)))
                           (exit-thread heartbeat-thread)
                           (thread-join! heartbeat-thread)
                           (exit-thread redraw-thread)
                           (thread-join! redraw-thread)
                           (exit-thread receive-thread)
                           (thread-join! receive-thread)
                           (receive (server-profile client-profile) (retrieve-profiles client)
                             (stop-profile client)
                             (with-task-mutex
                               (lambda ()
                                 (cond ((developer?)
                                        (feedback "Profile done")
                                        (close panel)
                                        (set! running-profile #f)
                                        (save-profile server-profile client-profile))
                                       (else
                                        (feedback "Profile saved")
                                        (close panel)
                                        (set! running-profile #f)
                                        (save-profile server-profile client-profile view-profile?: #f))))))))
                       'send))))
          (thread-base-priority-set! receive-thread udp-priority)
          (thread-base-priority-set! heartbeat-thread udp-priority)
          (thread-base-priority-set! redraw-thread udp-priority)
          (when send-thread
            (thread-base-priority-set! send-thread udp-priority))
          (thread-start! receive-thread)
          (thread-start! heartbeat-thread)
          (thread-start! redraw-thread)
          (when send-thread
            (thread-start! send-thread))
          (set! running-profile (new Network-Profile panel receive-thread heartbeat-thread redraw-thread send-thread)))))))


(definition protected (profile-heartbeat)
  (profile-network heartbeat?: #t))


(definition protected (save-profile server-profile client-profile (view-profile?: view-profile? #t))
  (let ((client (current-client)))
    (let ((parent {Directory Documents "profiles"}))
      (let ((dir (timestamped-directory parent "profile")))
        (create-directories dir)
        (save-binary (new-file dir "<server>.replay") server-profile)
        (save-binary (new-file dir (add-extension (get-character-name client) "replay")) client-profile)
        (when view-profile?
          (view-replay-directory dir))))))


(definition protected (done-profile)
  (let ((client (current-client))
        (world (current-world)))
    (receive (server-profile client-profile) (retrieve-profiles client)
      (terminate-profile)
      (save-profile server-profile client-profile)
      (display-message world "Profile done"))))


(definition protected (cancel-profile)
  (let ((world (current-world)))
    (terminate-profile)
    (display-message world "Profile cancelled")))


(definition protected (terminate-profile)
  (let ((client (current-client)))
    (let ((panel (get-panel running-profile))
          (receive-thread (get-receive-thread running-profile))
          (heartbeat-thread (get-heartbeat-thread running-profile))
          (redraw-thread (get-redraw-thread running-profile))
          (send-thread (get-send-thread running-profile)))
      (when send-thread
        (exit-thread send-thread)
        (thread-join! send-thread))
      (exit-thread heartbeat-thread)
      (thread-join! heartbeat-thread)
      (exit-thread redraw-thread)
      (thread-join! redraw-thread)
      (exit-thread receive-thread)
      (thread-join! receive-thread)
      (stop-profile client)
      (close panel)
      (set! running-profile #f)))))
