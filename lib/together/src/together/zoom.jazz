;;;============
;;;  Together
;;;============
;;;
;;;; Zoom
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module together.zoom jazz


(import (jazz.io)
        (jazz.platform))


;;;
;;;; Parse
;;;


(class Invalid-Link extends Exception
  
  
  (slot reason getter generate)
  
  
  (method override (initialize self rs)
    (nextmethod self)
    (set! reason rs))
  
  
  (method override (print self printer readably)
    (print-unreadable self printer
      (lambda (printer)
        (format printer "{s}" reason))))
  
  
  (method override (present-exception self)
    reason))


(definition (throw-invalid-link reason)
  (throw (new Invalid-Link reason)))


(definition protected (parse-link link)
  (define (trim link)
    (let ((targ "#success"))
      (let ((pos (search link targ)))
        (if (not pos)
            link
          (substring link 0 pos)))))
  
  (let ((targ "https://"))
    (let ((str (trim link)))
      (let ((pos (search str targ)))
        (if (not pos)
            (throw-invalid-link "Zoom link must contain https://")
          (let ((str (substring str (+ pos (string-length targ)) (string-length str))))
            (let ((targ "/j/"))
              (let ((pos (search str targ)))
                (if (not pos)
                    (throw-invalid-link "Zoom link must contain /j/")
                  (let ((server (substring str 0 pos))
                        (str (substring str (+ pos (string-length targ)) (string-length str))))
                    (let ((targ "?pwd="))
                      (let ((pos (search str targ)))
                        (if (not pos)
                            (values server str #f)
                          (values server (substring str 0 pos) (substring str (+ pos (string-length targ)) (string-length str))))))))))))))))


;;;
;;;; Launch
;;;


(definition public (launch-zoom-link link)
  (receive (server confno pwd) (parse-link link)
    (launch-zoom server confno pwd)))


(definition public (launch-zoom server confno pwd)
  (open-url (if (not pwd)
                (tie "zoommtg://zoom.us/join?confno={confno}")
              (tie "zoommtg://zoom.us/join?confno={confno}&pwd={pwd}")))))
