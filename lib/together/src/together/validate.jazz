;;;============
;;;  Together
;;;============
;;;
;;;; Together Validations
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module together.validate jazz


(import (jazz.component)
        (jazz.math)
        (jazz.stream)
        (world)
        (world.chronology)
        (world.client.udp)
        (world.context)
        (world.event)
        (world.evolution)
        (world.interface.timeline)
        (world.settings)
        (world.simulation)
        (world.udp)
        (together.community)
        (together.replay))


(definition public (test-ring media-kind suite)
  (declare (proper-tail-calls))
  (let ((world (current-world)))
    (bind (inserts events) suite
      (let ((origin 1)
            (receiver 2))
        (let ((client (simulate-udp-client receiver)))
          (let ((channel (require-receive-channel client origin media-kind)))
            (let ((ring (get-process-ring channel))
                  (average (new Average .05 1.))
                  (datas (make-table test: eqv?))
                  (base (current-seconds))
                  (evolution (new Evolution 10000))
                  (expected (new Evolution 10000)))
              (define (need-data kind origin sequence frame part total timestamp header?)
                (or (table-ref datas sequence #f)
                    (let ((header-size (+ 8 4 4 4 4 4 4 8 8 4)))
                      (let ((data (make-u8vector header-size))
                            (timestamp (if (not timestamp)
                                           -1.
                                         (flonum->timestamp timestamp))))
                        (write-udp-kind data kind)
                        (write-udp-origin data origin)
                        (write-udp-sequence data sequence)
                        (write-udp-frame data frame)
                        (write-udp-part data part)
                        (write-udp-total data total)
                        (write-udp-media-timestamp data timestamp)
                        (write-udp-media-header? data header?)
                        (table-set! datas sequence data)
                        data))))
              
              (define (compare-events)
                (for-each (lambda (info)
                            (bind (time abbrev data1 data2 data3 data4) info
                              (parameterize ((simulation-time (+ base time))
                                             (simulation-evolution expected))
                                (let ((id (evolution-abbrev->id abbrev)))
                                  (record-event id
                                                (fixnum->flonum origin)
                                                (fixnum->flonum media-kind)
                                                data1
                                                data2
                                                data3
                                                data4)))))
                          events))
              
              (let ((insert (new-thread
                              (lambda ()
                                (parameterize ((simulation? #t)
                                               (simulation-evolution evolution))
                                  (for-each (lambda (info)
                                              (bind (header? timestamp seconds sequence frame part total) info
                                                (let ((time (+ base seconds)))
                                                  (sleep (- time (current-seconds)))
                                                  (let ((data (need-data media-kind origin sequence frame part total timestamp header?)))
                                                    (receive-media client media-kind data)))))
                                            inserts))
                                (terminate ring))
                              'insert))
                    (process (new-thread
                               (lambda ()
                                 (parameterize ((simulation? #t)
                                                (simulation-evolution evolution))
                                   (let (looping)
                                     (let ((info (process-wait ring udp-process-window average)))
                                       (when (neq? info 'terminate)
                                         (process-info channel info)
                                         (looping))))))
                               'process)))
                (display-message world "Validating..." duration: +inf.0)
                (thread-start! insert)
                (thread-start! process)
                (thread-join! insert)
                (thread-join! process)
                (compare-events)
                (clear-message world)
                (view-evolution origin receiver evolution base)))))))))


(definition public (view-evolution origin receiver evolution base-time)
  (let ((zone (current-zone)))
    (let ((names (list->table (list (cons origin "Origin") (cons receiver "Receiver"))))
          (metadatas (list->table '()))
          (chronologies (list->table (list (cons receiver (new Chronology 10)))))
          (evolutions (list->table (list (cons receiver evolution)))))
      (let ((replay (new Replay "validate" #f receiver names metadatas chronologies evolutions base-time)))
        (view-replay zone replay)
        (let ((root (get-root zone)))
          (let ((panel (child root 'timeline)))
            (set-now panel (get-from panel))
            (now-update panel))))))))
